apply plugin: 'com.android.application'

def defaultAppVersionCode = '2'
def appVersionCode = System.getProperty('versionCode', defaultAppVersionCode).toInteger()

repositories {
    maven { url 'https://maven.google.com' }
    maven { url "http://artifactory.cloud.mapquest.com/artifactory/et-android-binaries"}
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.2'

    defaultConfig {
        applicationId "com.mapquest.navigation.sampleapp"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode appVersionCode
        versionName "3.3.0"
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    applicationVariants.all { variant ->
        variant.buildConfigField "String", "API_KEY", getApiKey()
        variant.resValue "string", "API_KEY", getApiKey()
    }
}

def getApiKey() {
    def props = new Properties()
    file("mapquest.properties").withInputStream { props.load(it) }
    return "\"" + props.getProperty("api_key") + "\""
}

dependencies {
    compile 'com.android.support:appcompat-v7:27.1.0'
    compile 'com.android.support.constraint:constraint-layout:1.1.0-beta2'
    compile 'com.android.support:design:27.1.0'

    implementation('com.mapquest:navigation:3.3.0')
    compile('com.mapzen.android:lost:3.0.4')            // used for GPS Location Acquisition (see notes)

    compile('com.mapquest:mapping-android-sdk:2.0.1') { // for Mapping SDK MapView; displays route to navigate
        exclude group: 'com.android.support'            // (note: in your app, you might omit this 'exclude')
    }

    compile('com.mapquest:search-ahead-v3:1.2.9')       // for the MapQuest "Search Ahead" feature (optional)
    // for butterknife, the well-known view binding lib
    compile 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    // multidex support lib (required when minSdkVersion is below 21)
    compile 'com.android.support:multidex:1.0.1'
}
